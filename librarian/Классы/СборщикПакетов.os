
#Использовать logos

Перем Лог;
Перем ЭтоВин;

Процедура СобратьФайлыПакета(Знач ГитРепо, Знач ВыходнойКаталог) Экспорт

	ФайлВерсии = СобратьПакет(ГитРепо, ОбъединитьПути(ТекущийКаталог(), ВыходнойКаталог));
	Если ПустаяСтрока(ФайлВерсии) Тогда
		Лог.Предупреждение("Не обнаружен результат сборки (.ospx) в каталоге " + ВыходнойКаталог);
		Возврат;
	КонецЕсли;

	СоздатьФайлБезСуффиксаВерсии(ФайлВерсии);

КонецПроцедуры

Функция СобратьПакет(Знач ГитРепо, Знач КаталогПакета)

	// Фича ОбъединитьПути. Если второй параметр - абсолютный путь, то возвращается он.
	КаталогРабочейКопии = ОбъединитьПути(ТекущийКаталог(), ГитРепо.ПолучитьКаталогРабочейКопии());

	Лог.Информация("Каталог пакета: %1", КаталогПакета);

	ИмяРепозитория = ГитРепо.ПолучитьИмяРепозитория();

	Выхлоп = ВременныеФайлы.НовоеИмяФайла();
	Если ЭтоВин Тогда
		СтрокаКоманды = СтрШаблон("cmd /C opm build %1 > %2", КаталогРабочейКопии, Выхлоп);
	Иначе
		СтрокаКоманды = СтрШаблон("bash -c ""opm build %1 > %2""", КаталогРабочейКопии, Выхлоп);
	КонецЕсли;
	КодВозврата = 0;
	Лог.Информация("Запускаю сборку %1", СтрокаКоманды);
	ЗапуститьПриложение(СтрокаКоманды, КаталогПакета, Истина, КодВозврата);

	ПакетУспешноСобран = КодВозврата = 0;
	Если ПакетУспешноСобран Тогда
		Лог.Информация("Пакет <%1> успешно собран", ИмяРепозитория);
	Иначе
		Лог.Ошибка("Ошибка сборки пакета <%1>", ИмяРепозитория);
		ТД = новый ТекстовыйДокумент;
		Если ЭтоВин Тогда
			ТД.Прочитать(Выхлоп, "cp866");
		Иначе
			ТД.Прочитать(Выхлоп);
		КонецЕсли;
		Лог.Ошибка(ТД.ПолучитьТекст());
	КонецЕсли;

	УдалитьФайлы(Выхлоп);

	НайденныеФайлы = НайтиФайлы(КаталогПакета, "*.ospx");
	Если НайденныеФайлы.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;

	Возврат НайденныеФайлы[0].ПолноеИмя;

КонецФункции

Процедура СоздатьФайлБезСуффиксаВерсии(Знач ФайлССуффиксом)
	
	ФайлОбъект = Новый Файл(ФайлССуффиксом);
	ИмяПакетаМассив = СтрРазделить(ФайлССуффиксом, "-");
	ИмяПакета = "";
	Для сч = 0 По ИмяПакетаМассив.ВГраница() - 1 Цикл
		ИмяПакета = ИмяПакета + ИмяПакетаМассив[сч] + "-";
	КонецЦикла;
	ИмяПакета = Лев(ИмяПакета, СтрДлина(ИмяПакета) - 1);

	КопироватьФайл(ФайлССуффиксом, ОбъединитьПути(ФайлОбъект.Путь, ИмяПакета) + ".ospx");

КонецПроцедуры

////////////////////////////////////////////////////////////////////

Лог = Логирование.ПолучитьЛог("oscript.infrastructure");
Си = Новый СистемнаяИнформация;
ЭтоВин = Найти(СИ.ВерсияОС, "Windows") > 0;
